plugins {
	id 'org.springframework.boot' version '3.1.1'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'java'
	id 'idea'
	id("maven-publish")
}


repositories {
	mavenCentral()
	maven {
		name = "GitHubPackages"
		url = uri("https://maven.pkg.github.com/onbelayconsulting/obcorelib")
		credentials {
			username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
			password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
		}
	}
}

dependencyManagement {

	imports {
		mavenBom 'org.springframework.boot:spring-boot-dependencies:3.1.1'
	}

	dependencies {

	}
}

sourceCompatibility = 17
targetCompatibility = 17

dependencies {
	implementation group: "com.onbelay.core", name: "obcore", version: "2.1.0"
	testImplementation group: "com.onbelay.core", name: "obcoretest", version: "2.1.0", classifier: 'tests'
	implementation("org.springframework.boot:spring-boot-starter-web")
	implementation('org.springframework.boot:spring-boot-starter-data-jpa')
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-webflux' // For WebClient
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation "com.fasterxml.jackson.core:jackson-core"
	implementation "com.fasterxml.jackson.core:jackson-databind"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-json-org"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
	implementation group: "org.apache.commons", name: "commons-lang3", version: "3.12.0"
	implementation group: "org.apache.commons", name: "commons-text", version: "1.+"
	implementation group: "org.apache.commons", name: "commons-collections4", version: "4.4"
	implementation group: 'org.postgresql', name: 'postgresql', version: '42.6.0'
	implementation group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '11.2.3.jre17'
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.security:spring-security-test')
	testImplementation("org.junit.vintage:junit-vintage-engine")
	implementation('org.springframework.boot:spring-boot-starter-log4j2')
}


configurations {
  all*.exclude group:'org.springframework.boot',
               module:'spring-boot-starter-logging'
	tests
}

bootJar {
  enabled = false
}

test {
	useJUnitPlatform()
}


jar {
	enabled = true
	archiveClassifier = ''
    archiveBaseName = 'obsharedlib'
	manifest {
			attributes("Implementation-Title": "OnBelay Shared Lib")
	}
}

task construct(type: Copy, dependsOn: assemble) {

	from 'build/libs'
	include '*.jar'
	into 'c:/softdev/repos/libs'
}

task testJar(type: Jar, dependsOn: testClasses) {
	archiveClassifier.set('tests')
	from sourceSets.test.output
}

artifacts {
	tests testJar
}


publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/onbelayconsulting/obsharedlib")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
				password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
			}
		}
	}
	publications {
		obcorelibrary(MavenPublication) {
			groupId = 'com.onbelay.shared'
			artifactId = 'obshared'
			version = System.getenv("SEMANTIC_VERSION") ?: project.findProperty("semantic_version")
			artifact jar
		}
		obcorelibraryTest(MavenPublication) {
			groupId = 'com.onbelay.shared'
			artifactId = 'obsharedtest'
			version = System.getenv("SEMANTIC_VERSION") ?: project.findProperty("semantic_version")
			artifact testJar
		}
	}
}

