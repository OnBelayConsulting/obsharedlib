plugins {
	id 'java'
	id 'org.liquibase.gradle' version '2.0.3'
	id("maven-publish")
}


repositories {
	mavenCentral()
}

dependencies {
	liquibaseRuntime "org.liquibase:liquibase-core:4.3.5"
	liquibaseRuntime 'org.postgresql:postgresql:42.2.8'
}

jar {
	archiveClassifier.set("database")
	manifest {
		attributes('Implementation-Title' : 'obshareddb')
	}

}

configurations {
	database
}

artifacts {
	database jar
}

task construct (group: "database", dependsOn: "distZip")

task distZip(type: Zip,
		group: 'build',
		description: 'Zip the Liquibase change sets.') {
	archiveFileName = 'obshareddb.zip'
	destinationDirectory = file("$buildDir/dist")
	from file("$projectDir/src")
	include '**/*.xml'
	include '**/*.sql'
}



task deployNew() {
	doLast{ liquibase.runList = "applySnapshotActivity, applyChangesActivity" }
}
deployNew.finalizedBy update

task deployChanges() {
	doLast { liquibase.runList = "applyChangesActivity" }
}
deployChanges.finalizedBy update


liquibase {

	Properties jdbcProps = new Properties()
	File jdbcPropertiesFile = project.file('jdbc.properties')
	if (!jdbcPropertiesFile.exists()) {
		jdbcProps.setProperty("jdbc.url", "")
		jdbcProps.setProperty("jdbc.username", "")
		jdbcProps.setProperty("jdbc.password", "")
		jdbcProps.setProperty("jdbc.driverClassName", "")
	} else {
		jdbcProps.load(jdbcPropertiesFile.newDataInputStream())
	}
	
	System.getProperties().put("tblspace_data", tblspace_data)
	System.getProperties().put("tblspace_data_idx",  tblspace_data_idx)
	System.getProperties().put("tblspace_work",  tblspace_work)
	System.getProperties().put("tblspace_work_idx",  tblspace_work_idx)

	activities {

		noChangeLogActivity {
			url jdbcProps.get("jdbc.url")
			username jdbcProps.get("jdbc.username")
			password jdbcProps.get("jdbc.password")
		}

		applySnapshotActivity {
			println(project.projectDir.toPath())
			changeLogFile  '/src/main/resources/obshared/snapshot/changelog.xml'
			url jdbcProps.get("jdbc.url")
			username jdbcProps.get("jdbc.username")
			password jdbcProps.get("jdbc.password")
		}

		applyChangesActivity {
			changeLogFile  '/src/main/resources/obshared/changes/changelog.xml'
			url jdbcProps.get("jdbc.url")
			username jdbcProps.get("jdbc.username")
			password jdbcProps.get("jdbc.password")
		}
	}

	try {
		runList = project.ext.runList
	} catch (Exception e) {
		runList = 'noChangeLogActivity'
	}
}


publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/onbelayconsulting/obshareddb")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
			}
		}
	}
	publications {
		obcorelibrary(MavenPublication) {
			groupId = 'com.onbelay.shared'
			artifactId = 'obshareddb'
			version = System.getenv("SEMANTIC_VERSION") ?: project.findProperty("semantic_version")
			artifact jar
		}
	}
}
